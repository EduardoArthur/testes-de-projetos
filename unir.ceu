#include "random.ceu"

emit FRAMES_SET(yes);

event int collision;
event int gameOver;

code/await Pixel (none) -> (var Point pt) ->none do
    pt = call Random_Point_In_Square(-25,-25,25,17);
    var int frame = 0;
    par/or do
        every 50ms do
            frame = (frame + 1) % 9;
        end
    with
        every FRAMES_REDRAW do
            emit GRAPHICS_SET_BMP_FRAME(frame,9);
            emit GRAPHICS_SET_SCALE(0.2,0.2);

            emit GRAPHICS_DRAW_BMP(pt.x,pt.y,"res/coin.bmp");
        end
    with
        await outer.gameOver;
    end
end

code/await Player(var int id, var int up, var int right, var int down, var int left, var Color color) -> (var Point pt) ->none do //declara a variavel visivel
    pt = val Point(0,0);
    var int score = 0;
    var int x_axis = 0;
    var int y_axis = 0;
    var int frame = 0;
    par/or do
        var int key;
        every key in KEY_PRESS do
            //atualiza x_axis e y_axis com 1 ou -1
            if (key == left) then
                x_axis = -1;
            else/if (key == right) then
                x_axis = 1;
            else/if (key == up) then
                y_axis = 1;
            else/if (key == down) then
                y_axis = -1;
            end 
        end
    with
        var int key;
        every key in KEY_UNPRESS do
            //atualiza x_axis e y_axis com 0
            if (key == left) or (key == right) then
                x_axis = 0;
            else/if (key == up) or (key == down) then
                y_axis = 0;
            end 
        end        
    with
        every 80ms do
            //atualiza pt com base em x_axis e y_axis
            if (x_axis == 1) and (pt.x < 25) then
                pt.x = pt.x + 1;
                frame = 1;
            else/if (x_axis == -1) and (pt.x > -25) then
                pt.x = pt.x - 1;
                frame = 2;
            end

            if (y_axis == 1) and (pt.y < 25) then
                pt.y = pt.y + 1;
                frame = 3;
            else/if (y_axis == -1) and (pt.y > -25) then
                pt.y = pt.y - 1;
                frame = 0;
            end
        end
    with
        every FRAMES_REDRAW do
            //desenha pixel
            emit GRAPHICS_SET_BMP_FRAME(frame,4);
            emit GRAPHICS_SET_SCALE(0.2,0.2);
            if(id == 1)then
                emit GRAPHICS_DRAW_BMP(pt.x,pt.y,"res/player1.bmp");
            else
                emit GRAPHICS_DRAW_BMP(pt.x,pt.y,"res/player2.bmp");
            end
            
            emit GRAPHICS_SET_SCALE(1,1);
            emit GRAPHICS_SET_COLOR_NAME(color);
            if (id == 1) then
                emit GRAPHICS_DRAW_INT(-21,21,score);
            else
                emit GRAPHICS_DRAW_INT(23,21,score);
            end
        end
    with
        loop do
            var int r_id = await outer.collision until r_id == id;
            score = score+1;
            _printf("%d - score %d\n", id, score);
            if(score == 1)then
                emit outer.gameOver(id);
            end
        end
    with
        await outer.gameOver;
    end
end

pool[2] Player players;
spawn Player(1, KEY_UP, KEY_RIGHT, KEY_DOWN, KEY_LEFT, COLOR_BLUE) in players;
spawn Player(2, KEY_w , KEY_d    , KEY_s   , KEY_a   , COLOR_RED) in players;

pool[5] Pixel pixels;

var int idV;
par/or do
    every 1s do
        spawn Pixel() in pixels;
    end
with
    every FRAMES_UPDATE do
        var&? Player player;
        loop player in players do
            var&? Pixel pixel; //pode nao existir
            loop pixel in pixels do
                if( (player!.pt.x == pixel!.pt.x) and (player!.pt.y == pixel!.pt.y) )then
                    kill pixel;
                    emit collision(player!.id);
                end
            end
        end
    end
with
    idV = await gameOver;
end

every FRAMES_REDRAW do
    emit GRAPHICS_SET_SCALE(1,1);
    emit GRAPHICS_DRAW_TEXT(0,0,"Game Over");
    emit GRAPHICS_DRAW_INT(0,-10,idV);
end